plugins {
    id 'java'
    id 'signing'
    id("io.github.goooler.shadow") version "8.1.8"
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'io.github.goooler.shadow'
    apply plugin: 'maven-publish'

    group = 'com.envyful.api'
    version = '7.1.7'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    publish.dependsOn(':commons:test')
    publish.dependsOn("javadoc")

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name = "envyware"
                    url = uri("https://maven.envyware.co.uk/releases")
                    credentials(PasswordCredentials)
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
            publications {
                maven(MavenPublication) {
                    groupId = "com.envyful.api"
                    artifactId = project.name
                    version = project.version
                    from components.java
                }
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.lucko.me/' }
        maven { url = "https://repo.aikar.co/content/groups/aikar/" }
        maven { url = "https://jitpack.io" }
        maven {
        	name = 'sponge'
        	url = 'https://repo.spongepowered.org/maven'
    	}
        maven {
            url = "https://maven.envyware.co.uk/releases"
        }
    }
}
